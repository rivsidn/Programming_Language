         ;´úÂëÇåµ¥17-1
         ;ÎÄ¼þÃû£ºc17_mbr.asm
         ;ÎÄ¼þËµÃ÷£ºÓ²ÅÌÖ÷Òýµ¼ÉÈÇø´úÂë 
         ;´´½¨ÈÕÆÚ£º2012-07-13 11:20        ;ÉèÖÃ¶ÑÕ»¶ÎºÍÕ»Ö¸Õë 
         
         core_base_address equ 0x00040000   ;³£Êý£¬ÄÚºË¼ÓÔØµÄÆðÊ¼ÄÚ´æµØÖ· 
         core_start_sector equ 0x00000001   ;³£Êý£¬ÄÚºËµÄÆðÊ¼Âß¼­ÉÈÇøºÅ 

;===============================================================================
SECTION  mbr  vstart=0x00007c00         

         mov ax,cs      
         mov ss,ax
         mov sp,0x7c00
      
         ;¼ÆËãGDTËùÔÚµÄÂß¼­¶ÎµØÖ·
         mov eax,[cs:pgdt+0x02]             ;GDTµÄ32Î»ÎïÀíµØÖ· 
         xor edx,edx
         mov ebx,16
         div ebx                            ;·Ö½â³É16Î»Âß¼­µØÖ· 

         mov ds,eax                         ;ÁîDSÖ¸Ïò¸Ã¶ÎÒÔ½øÐÐ²Ù×÷
         mov ebx,edx                        ;¶ÎÄÚÆðÊ¼Æ«ÒÆµØÖ· 

         ;Ìø¹ý0#ºÅÃèÊö·ûµÄ²ÛÎ» 
         ;´´½¨1#ÃèÊö·û£¬±£»¤Ä£Ê½ÏÂµÄ´úÂë¶ÎÃèÊö·û
         mov dword [ebx+0x08],0x0000ffff    ;»ùµØÖ·Îª0£¬½çÏÞ0xFFFFF£¬DPL=00 
         mov dword [ebx+0x0c],0x00cf9800    ;4KBÁ£¶È£¬´úÂë¶ÎÃèÊö·û£¬ÏòÉÏÀ©Õ¹ 

         ;´´½¨2#ÃèÊö·û£¬±£»¤Ä£Ê½ÏÂµÄÊý¾Ý¶ÎºÍ¶ÑÕ»¶ÎÃèÊö·û 
         mov dword [ebx+0x10],0x0000ffff    ;»ùµØÖ·Îª0£¬½çÏÞ0xFFFFF£¬DPL=00
         mov dword [ebx+0x14],0x00cf9200    ;4KBÁ£¶È£¬Êý¾Ý¶ÎÃèÊö·û£¬ÏòÉÏÀ©Õ¹ 

         ;³õÊ¼»¯ÃèÊö·û±í¼Ä´æÆ÷GDTR
         mov word [cs: pgdt],23             ;ÃèÊö·û±íµÄ½çÏÞ   
 
         lgdt [cs: pgdt]
      
         in al,0x92                         ;ÄÏÇÅÐ¾Æ¬ÄÚµÄ¶Ë¿Ú 
         or al,0000_0010B
         out 0x92,al                        ;´ò¿ªA20

         cli                                ;ÖÐ¶Ï»úÖÆÉÐÎ´¹¤×÷

         mov eax,cr0                  
         or eax,1
         mov cr0,eax                        ;ÉèÖÃPEÎ»
      
         ;ÒÔÏÂ½øÈë±£»¤Ä£Ê½... ...
         jmp dword 0x0008:flush             ;16Î»µÄÃèÊö·ûÑ¡Ôñ×Ó£º32Î»Æ«ÒÆ
                                            ;ÇåÁ÷Ë®Ïß²¢´®ÐÐ»¯´¦ÀíÆ÷
         [bits 32]               
  flush:                                  
         mov eax,0x00010                    ;¼ÓÔØÊý¾Ý¶Î(4GB)Ñ¡Ôñ×Ó
         mov ds,eax
         mov es,eax
         mov fs,eax
         mov gs,eax
         mov ss,eax                         ;¼ÓÔØ¶ÑÕ»¶Î(4GB)Ñ¡Ôñ×Ó
         mov esp,0x7000                     ;¶ÑÕ»Ö¸Õë
         
         ;ÒÔÏÂ¼ÓÔØÏµÍ³ºËÐÄ³ÌÐò
         mov edi,core_base_address

         mov eax,core_start_sector
         mov ebx,edi                        ;ÆðÊ¼µØÖ·
         call read_hard_disk_0              ;ÒÔÏÂ¶ÁÈ¡³ÌÐòµÄÆðÊ¼²¿·Ö£¨Ò»¸öÉÈÇø£©

         ;ÒÔÏÂÅÐ¶ÏÕû¸ö³ÌÐòÓÐ¶à´ó
         mov eax,[edi]                      ;ºËÐÄ³ÌÐò³ß´ç
         xor edx,edx
         mov ecx,512                        ;512×Ö½ÚÃ¿ÉÈÇø
         div ecx

         or edx,edx
         jnz @1                             ;Î´³ý¾¡£¬Òò´Ë½á¹û±ÈÊµ¼ÊÉÈÇøÊýÉÙ1
         dec eax                            ;ÒÑ¾­¶ÁÁËÒ»¸öÉÈÇø£¬ÉÈÇø×ÜÊý¼õ1
   @1:
         or eax,eax                         ;¿¼ÂÇÊµ¼Ê³¤¶È¡Ü512¸ö×Ö½ÚµÄÇé¿ö
         jz pge                             ;EAX=0 ?

         ;¶ÁÈ¡Ê£ÓàµÄÉÈÇø
         mov ecx,eax                        ;32Î»Ä£Ê½ÏÂµÄLOOPÊ¹ÓÃECX
         mov eax,core_start_sector
         inc eax                            ;´ÓÏÂÒ»¸öÂß¼­ÉÈÇø½Ó×Å¶Á
   @2:
         call read_hard_disk_0
         inc eax
         loop @2                            ;Ñ­»·¶Á£¬Ö±µ½¶ÁÍêÕû¸öÄÚºË

   pge:
         ;×¼±¸´ò¿ª·ÖÒ³»úÖÆ¡£´Ó´Ë£¬ÔÙÒ²²»ÓÃÔÚ¶ÎÖ®¼ä×ªÀ´×ªÈ¥£¬ÊµÔÚÔÎºõ~ 
         
         ;´´½¨ÏµÍ³ÄÚºËµÄÒ³Ä¿Â¼±íPDT
         mov ebx,0x00020000                 ;Ò³Ä¿Â¼±íPDTµÄÎïÀíµØÖ·
         
         ;ÔÚÒ³Ä¿Â¼ÄÚ´´½¨Ö¸ÏòÒ³Ä¿Â¼±í×Ô¼ºµÄÄ¿Â¼Ïî
         mov dword [ebx+4092],0x00020003 

         mov edx,0x00021003                 ;MBR¿Õ¼äÓÐÏÞ£¬ºóÃæ¾¡Á¿²»Ê¹ÓÃÁ¢¼´Êý
         ;ÔÚÒ³Ä¿Â¼ÄÚ´´½¨ÓëÏßÐÔµØÖ·0x00000000¶ÔÓ¦µÄÄ¿Â¼Ïî
         mov [ebx+0x000],edx                ;Ð´ÈëÄ¿Â¼Ïî£¨Ò³±íµÄÎïÀíµØÖ·ºÍÊôÐÔ£©      
                                            ;´ËÄ¿Â¼Ïî½öÓÃÓÚ¹ý¶É¡£
         ;ÔÚÒ³Ä¿Â¼ÄÚ´´½¨ÓëÏßÐÔµØÖ·0x80000000¶ÔÓ¦µÄÄ¿Â¼Ïî
         mov [ebx+0x800],edx                ;Ð´ÈëÄ¿Â¼Ïî£¨Ò³±íµÄÎïÀíµØÖ·ºÍÊôÐÔ£©

         ;´´½¨ÓëÉÏÃæÄÇ¸öÄ¿Â¼ÏîÏà¶ÔÓ¦µÄÒ³±í£¬³õÊ¼»¯Ò³±íÏî 
         mov ebx,0x00021000                 ;Ò³±íµÄÎïÀíµØÖ·
         xor eax,eax                        ;ÆðÊ¼Ò³µÄÎïÀíµØÖ· 
         xor esi,esi
  .b1:       
         mov edx,eax
         or edx,0x00000003                                                      
         mov [ebx+esi*4],edx                ;µÇ¼ÇÒ³µÄÎïÀíµØÖ·
         add eax,0x1000                     ;ÏÂÒ»¸öÏàÁÚÒ³µÄÎïÀíµØÖ· 
         inc esi
         cmp esi,256                        ;½öµÍ¶Ë1MBÄÚ´æ¶ÔÓ¦µÄÒ³²ÅÊÇÓÐÐ§µÄ 
         jl .b1
         
         ;ÁîCR3¼Ä´æÆ÷Ö¸ÏòÒ³Ä¿Â¼£¬²¢ÕýÊ½¿ªÆôÒ³¹¦ÄÜ 
         mov eax,0x00020000                 ;PCD=PWT=0
         mov cr3,eax

         ;½«GDTµÄÏßÐÔµØÖ·Ó³Éäµ½´Ó0x80000000¿ªÊ¼µÄÏàÍ¬Î»ÖÃ 
         sgdt [pgdt]
         mov ebx,[pgdt+2]
         add dword [pgdt+2],0x80000000      ;GDTRÒ²ÓÃµÄÊÇÏßÐÔµØÖ·
         lgdt [pgdt]

         mov eax,cr0
         or eax,0x80000000
         mov cr0,eax                        ;¿ªÆô·ÖÒ³»úÖÆ
   
         ;½«¶ÑÕ»Ó³Éäµ½¸ß¶Ë£¬ÕâÊÇ·Ç³£ÈÝÒ×±»ºöÂÔµÄÒ»¼þÊÂ¡£Ó¦µ±°ÑÄÚºËµÄËùÓÐ¶«Î÷
         ;¶¼ÒÆµ½¸ß¶Ë£¬·ñÔò£¬Ò»¶¨»áºÍÕýÔÚ¼ÓÔØµÄÓÃ»§ÈÎÎñ¾Ö²¿¿Õ¼äÀïµÄÄÚÈÝ³åÍ»£¬
         ;¶øÇÒºÜÄÑÏëµ½ÎÊÌâ»á³öÔÚÕâÀï¡£ 
         add esp,0x80000000                 
                                             
         jmp [0x80040004]  
       
;-------------------------------------------------------------------------------
read_hard_disk_0:                           ;´ÓÓ²ÅÌ¶ÁÈ¡Ò»¸öÂß¼­ÉÈÇø
                                            ;EAX=Âß¼­ÉÈÇøºÅ
                                            ;DS:EBX=Ä¿±ê»º³åÇøµØÖ·
                                            ;·µ»Ø£ºEBX=EBX+512 
         push eax 
         push ecx
         push edx
      
         push eax
         
         mov dx,0x1f2
         mov al,1
         out dx,al                          ;¶ÁÈ¡µÄÉÈÇøÊý

         inc dx                             ;0x1f3
         pop eax
         out dx,al                          ;LBAµØÖ·7~0

         inc dx                             ;0x1f4
         mov cl,8
         shr eax,cl
         out dx,al                          ;LBAµØÖ·15~8

         inc dx                             ;0x1f5
         shr eax,cl
         out dx,al                          ;LBAµØÖ·23~16

         inc dx                             ;0x1f6
         shr eax,cl
         or al,0xe0                         ;µÚÒ»Ó²ÅÌ  LBAµØÖ·27~24
         out dx,al

         inc dx                             ;0x1f7
         mov al,0x20                        ;¶ÁÃüÁî
         out dx,al

  .waits:
         in al,dx
         and al,0x88
         cmp al,0x08
         jnz .waits                         ;²»Ã¦£¬ÇÒÓ²ÅÌÒÑ×¼±¸ºÃÊý¾Ý´«Êä 

         mov ecx,256                        ;×Ü¹²Òª¶ÁÈ¡µÄ×ÖÊý
         mov dx,0x1f0
  .readw:
         in ax,dx
         mov [ebx],ax
         add ebx,2
         loop .readw

         pop edx
         pop ecx
         pop eax
      
         ret

;-------------------------------------------------------------------------------
         pgdt             dw 0
                          dd 0x00008000     ;GDTµÄÎïÀí/ÏßÐÔµØÖ·
;-------------------------------------------------------------------------------                             
         times 510-($-$$) db 0
                          db 0x55,0xaa